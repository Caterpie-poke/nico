Source
    = _ {
    $Title _
    ($VariableDecl _)?
    ($FunctionDecl _)*
    #Input
    } EOF

_            = ( S / BLOCKCOMMENT / LINECOMMENT )*
__           = ( S / BLOCKCOMMENT / LINECOMMENT )+
S            = [ \t\n\r] / '　'
BLOCKCOMMENT = '/*' ( !'*/' . )* '*/'
             / '(*' ( !'*)' . )* '*)'
LINECOMMENT  = '//' ( !EOL . )* EOL
EOF          = !.
EOL          = ('\r'? '\n') / EOF

"," = ',' / '、'
"(" = '(' / '（'
")" = ')' / '）'
"<" = '<' / '＜'
">" = '>' / '＞'
"+" = '+' / '＋'
"-" = '-' / 'ー'
"*" = '*' / '＊'
"/" = '/' / '／'
"**" = "*" "*"
"%" = '%' / '％'
"=" = '=' / '＝'
" " = ' ' / '　'
"@" = '@' / '＠'
":" = ':' / '：'
">=" = '>=' / '＞＝'
"<=" = '<=' / '＜＝'
"NOT=" = 'NOT=' / 'NOT＝'

C
    = HIRA
    / KATA
    / KANJI
    / MARK
    / Alphabet
    / [a-zA-Z0-9]

Any
    = [a-zA-Z0-9]

HIRA = [ぁ-ん]
KATA = [ァ-ヶ]
KANJI = [㐀-䶵一-龠々〇〻]
MARK = [ー]
Alphabet = [ａ-ｚＡ-Ｚ０-９]

/*-------------------------PreDescription-------------------------*/
Title
    = '「' InnerTitle '」'
InnerTitle
    = {(!'」' C)+ #Title}

FirstArticle
    = '第0条'
    / '第０条'
Article
    = '第' Digit '条'

/*-------------------------VariableDecl-------------------------*/
VariableDecl
    = {
    FirstArticle _ C+ _
    (VarDecl _)*
    '終了' _
    #VDecl}

VarDecl
    = MapDecl
    / Identifier

//MapDecl created by dictParse
MapParam
    = "(" (!")" C)+ ")"
    / '〜'

/*-------------------------FunctionDecl-------------------------*/
FunctionDecl
    = {
    Article _ $FuncDeclName _
    ($FuncDeclInput)? _
    ($FuncDeclRequire)? _
    ($FuncDeclBody)? _
    ($FuncDeclOutput)? _
    '終了'
    #FDecl}
    / SolidityBlock

FuncDeclName
    = {$FuncName (_ "(" _ $FuncSolName _ ")")? #FDName}
FuncName
    = {(!"(" C)+ #FName}
FuncSolName
    = {[a-zA-Z] [a-zA-Z0-9]* #FSName}

FuncDeclInput
    = {
    '入力' ":" _
    ($Identifier _)+
    '終了'
    #FDInput}

FuncDeclRequire
    = {
    '要件' ":" _
    ($Expression _)+
    '終了'
    #FDRequire}

FuncDeclBody
    = {
    '本文' ":" _
    ($Statement _)+
    '終了'
    #FDBody}

FuncDeclOutput
    = {
    '出力' ":" _
    ($Expression _)+
    '終了'
    #FDOutput}

SolidityBlock
    =
    Article _ SOL EOL
    SolBlockInner _
    '終了'

SOL
    = 'Solidity'
    / 'SOLIDITY'
    / 'solidity'
    / 'Sol'
    / 'SOL'
    / 'sol'

SolBlockInner
    = {(!'終了' .)+ #Sol}


/*-------------------------Statement-------------------------*/
Statement
    = IfStatement
    / SolidityStatement
    / Assign
    / LocalVariableDecl
    / Expression

IfStatement
    = {
    'もし' _ $Condition _ 'ならば' _
    $Then
    ($ElseIf)*
    'でなければ'
    $Else
    '終了'
    #If}
Condition
    = {$LogicalExpr #Cond}
Then
    = {($Statement _)+ #Then}
ElseIf
    = {
    'ではなく、もし' _ $Condition _ 'ならば' _
    $Then
    #ElIf
    }
Else
    = {($Statement _)+ #Else}

SolidityStatement
    = 'SOL{' SolInner '}'
SolInner
    = {(!'}' !EOL .)* #Sol}

Assign
    = {$AssignLeft _ 'は' _ $AssignRight (_ 'とする')? #Assign}
    / {$AssignLeft _ 'を' _ $AssignRight _ 'とする' #Assign}
AssignLeft
    = Tuple
    / PostfixExpr
AssignRight
    = Tuple
    / Expression

//Only used in Assign
Tuple
    = {"(" _ $PrimaryExpr (_ "," _ $PrimaryExpr)+ _ ")" #Tuple}

LocalVariableDecl
    = {($Expression _ 'として' _)? $Identifier _ 'をおく' #LocalVDecl}

/*-------------------------Expression-------------------------*/
Expression
    = FunctionExpression
    / LogicalExpr

/*-------------------------FunctionExpr-------------------------*/
FunctionExpression
    = {
    ($FuncExprParam (_ "," _ $FuncExprParam)* _ 'に対し' _ )? $FuncExprName _ ('を行う' / 'を行なった結果')
    #FExpr}
FuncExprParam
    = {$PostfixExpr #FEParam}
FuncExprName
    = {!Preserved (!'を行う' !'を行なった結果' C)+ #FEName}
Preserved
    = 'とする'
    / 'として'
    / 'ならば'
    / 'でなければ'
    / 'ではなく、もし'
    / 'の'
    / ","
    / '終了'
    / [0-9]+
    / "+" / "-" / "*" / "/"
    / "<" / ">" / "<=" / ">="
    / "=" / "NOT="
    / 'かつ'
    / 'または'

/*-------------------------LogicalExpr-------------------------*/
LogicalExpr
    = {$EqualityExpr (_ $({'かつ' #AND}/{'または' #OR}) _ $EqualityExpr)* #Logical}

EqualityExpr
    = {$RelationalExpr (_ $({"=" #EQ}/{"NOT=" #NEQ}) _ $RelationalExpr)* #Equality}

RelationalExpr
    = {$AddSubExpr (_ $CMPR _ $AddSubExpr)* #Relational}
CMPR
    = {">="#GTE}
    / {"<="#LTE}
    / {">"#GT}
    / {"<"#LT}

AddSubExpr
    = {$MulDivExpModExpr (_ $({"+" #ADD}/{"-" #SUB}) _ $MulDivExpModExpr)* #AddSub}
MulDivExpModExpr
    = {$PostfixExpr (_ $({"*" #MUL}/{"/" #DIV}/{"**" #EXP}/{"%" #MOD}) _ $PostfixExpr)* #MulDivExpMod}

/*-------------------------PostfixExpr-------------------------*/
PostfixExpr
    = StructRef
    / ArrayRef
    / MapRef
    / PrimaryExpr

StructRef
    = {$StructComponent ('の' $StructComponent)+ #StructRef}
StructComponent
    = ArrayRef
    / MapRef
    / Identifier

ArrayRef
    = {$ArrayComponent ('の' $Index '番目')+ #ArrayRef}
ArrayComponent
    = MapRef
    / Identifier
Index
    = {$(Num / Identifier) #Index}

PrimaryExpr
    = Address
    / Num
    / Boolean
    / String
    / Identifier

Num
    = {Digit #Int}
Digit
    = [1-9] [0-9]*
    / ([0] ![0-9])

Address
    = {'0x'
    [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9]
    [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9] [0-9]
    #Addr}
    / {'0x0' ![0-9] #Addr}

Boolean
    = {'はい' #BT}
    / {'いいえ' #BF}
    / {'true' #BT}
    / {'false' #BF}

String
    = {'「' (!'「' !'」' C)+ '」' #String}
    / {'"' (!'"' C)+ '"' #String}

Identifier
    = {FromDict #Id}

MapRef
    = {$PrimaryExpr 'の投票者情報' #Map0}
    / {$PrimaryExpr 'が' $PrimaryExpr 'から動かせる金額' #Map1}
    / {$PrimaryExpr 'から' $PrimaryExpr 'が送金可能な金額' #Map2}
    / {$PrimaryExpr 'の残高' #Map3}
    / {$PrimaryExpr 'の数字表' #Map4}

MapDecl
    = {MapParam 'の投票者情報' #DMap0}
    / {MapParam 'が' MapParam 'から動かせる金額' #DMap1}
    / {MapParam 'から' MapParam 'が送金可能な金額' #DMap2}
    / {MapParam 'の残高' #DMap3}
    / {MapParam 'の数字表' #DMap4}

FromDict
    = '総発行量の指定値'
    / 'built-in'
    / 'built-in'
    / 'built-in'
    / 'built-in'
    / '◯×テーブル'
    / 'クラス名簿'
    / 'ナンバーズ'
    / 'Alice'
    / 'メッセージ'
    / 'map4'
    / 'map3'
    / 'map2'
    / 'map1'
    / 'map0'
    / 'チェック'
    / '総発行量'
    / '被送金者'
    / 'Name'
    / 'ほげげ'
    / '管理者'
    / '野菜名'
    / '指定量'
    / '創設者'
    / '送金額'
    / '対象者'
    / '指定額'
    / '送金者'
    / 'あなた'
    / '実行者'
    / '単語4'
    / '単語3'
    / '単語2'
    / '単語1'
    / '数値'
    / 'ID'
